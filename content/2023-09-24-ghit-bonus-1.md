---
title: "Building ghit - Bonus 1"
taxonomies:
  tags: ["building_git", "programming", "rust", "bazel"]
---

I wanted to examine `Bazel` in my free time, and it may be a good fit for a multi-language and multi-platform project I'm working on. And that's what Bazel is trying to solve.

So here we go, `bazel` in `ghit` :D

## Workspace Setup

I've installed `bazel` using `bazelisk`, then created `WORKSPACE.bazel` at the project's root dir.

Then I added the `rules_rust` by loading the `http_archive`.

```starlark
# ./WORKSPACE.bazel
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "rules_rust",
    sha256 = "db89135f4d1eaa047b9f5518ba4037284b43fc87386d08c1d1fe91708e3730ae",
    urls = ["https://github.com/bazelbuild/rules_rust/releases/download/0.27.0/rules_rust-v0.27.0.tar.gz"],
)
```

`load` is similar to `import` in other languages. And the params of the `http_archive` I got those from `rules_rust` GitHub release. <https://github.com/bazelbuild/rules_rust/releases>

Now add `rules_rust` dependencies using `rules_rust_dependencies()` and register the rust toolchain.

We can specify release channels, editions, and versions in the toolchain. I've only selected 2021's edition.

```starlark
# ./WORKSPACE.bazel
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "rules_rust",
    sha256 = "db89135f4d1eaa047b9f5518ba4037284b43fc87386d08c1d1fe91708e3730ae",
    urls = ["https://github.com/bazelbuild/rules_rust/releases/download/0.27.0/rules_rust-v0.27.0.tar.gz"],
)

load("@rules_rust//rust:repositories.bzl", "rules_rust_dependencies", "rust_register_toolchains")

rules_rust_dependencies()

rust_register_toolchains(
    edition = "2021",
)
```

Then, we need to list the dependencies we're using. For example, if we're using `anyhow`, we should put that in the `WORKSPACE.bazel`, but this is painful, so we'll rely on `crate_universe` to do that for us!

```starlark
load("@rules_rust//crate_universe:repositories.bzl", "crate_universe_dependencies")

crate_universe_dependencies()

load("@rules_rust//crate_universe:defs.bzl", "crates_repository")

crates_repository(
    name = "crate_index",
    cargo_lockfile = "//:Cargo.lock",
    lockfile = "//:cargo-bazel-lock.json",
    manifests = [
        "//:Cargo.toml",
        "//:ghit_core/Cargo.toml",
        "//:ghit_cli/Cargo.toml",
    ],
)

load("@crate_index//:defs.bzl", "crate_repositories")

crate_repositories()
```

We gave the `crates_repository` a custom name, `crate_index`, and we specified the `cargo_lockfile` to be the actual `Cargo.lock` file since we're using a cargo workspace then there's only this single lock file. And we should specify bazel's lockfile and create that later. And finally, set the `Cargo.toml` files. Bazel can automatically generate dependencies.

Then I created `cargo-bazel-lock.json`.

```shell
touch cargo-bazel-lock.json
```

And I've pinned the dependencies to that file.

```shell
CARGO_BAZEL_REPIN=1 bazel sync --only=crate_index
```

When dependencies change, we should sync again.

## Library Setup

Now we want to setup our lib, which is `ghit_core`.

```starlark
# ./ghit_core/BUILD.bazel

load("@crate_index//:defs.bzl", "aliases", "all_crate_deps")
load("@rules_rust//rust:defs.bzl", "rust_library")

rust_library(
    name = "ghit_core",
    srcs = glob([
        "src/**/*.rs",
    ]),
    aliases = aliases(),
    deps = all_crate_deps(),
    visibility = ["//visibility:public"],
)
```

Here, I've included the `all_crate_deps` to also generate dependencies depending on what we provide in `Cargo.toml`, and the `aliases` here are needed in case we've used dependency renaming, say:

```toml
ghit_lib = { package = "ghit_core", path = "../ghit_core" }
```

Otherwise, we need to define our aliases explicitly.

And finally, we've included the `rust_library` to build a rust lib. We give it a name, sources to be everything with `.rs` inside `src/` recursively, and public visibility.

## Binary Setup

Similar to the lib setup, we add the `ghit_core` as a dependency and use `rust_binary` instead of `rust_library`.

```starlark
load("@crate_index//:defs.bzl", "aliases", "all_crate_deps")
load("@rules_rust//rust:defs.bzl", "rust_binary")

rust_binary(
    name = "ghit_cli",
    srcs = glob([
        "src/**/*.rs",
    ]),
    aliases = aliases(),
    deps = all_crate_deps() + [
        "//ghit_core"
    ],
    visibility = ["//visibility:public"],
)
```

Finally, I created `BUILD.bazel` in the root dir, beside `WORKSPACE.bazel`, and I ran:

```shell
bazel build //ghit_cli:ghit_cli
./bazel-bin/ghit_cli/ghit_cli
```

---

## Credits

Thanks to <https://www.tweag.io/blog/2023-07-27-building-rust-workspace-with-bazel/>. It helped a lot! (There is some missing stuff in their tutorial, but they provided a link to the repo, so I was able to fill in the blank).
